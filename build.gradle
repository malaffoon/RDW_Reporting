import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

buildscript {
    ext {
        springBootVersion = '1.5.4.RELEASE'
    }
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath("com.bmuschko:gradle-docker-plugin:3.0.11")
        classpath("io.spring.gradle:dependency-management-plugin:1.0.0.RELEASE")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('org.springframework.build.gradle:propdeps-plugin:0.0.7')
    }
}

plugins {
    id 'com.jfrog.artifactory' version '4.4.18'
}

apply plugin: 'io.spring.dependency-management'

// Set version dependencies. This allows the cli to force version, e.g. `-Pcommon=1.1.0-SNAPSHOT`
String rdwCommonVersion = project.hasProperty("common") ? project.property("common") : "1.2.0-101"
String rdwSchemaVersion = project.hasProperty("schema") ? project.property("schema") : "1.2.0-390"
String dockerPrefix = "smarterbalanced"

allprojects {
    group = 'org.opentestsystem.rdw.reporting'
    version = '1.2.1-SNAPSHOT'

    apply plugin: 'idea'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: "com.jfrog.artifactory"

    if (project.hasProperty("buildNumber")) {
        project.setVersion(project.version.toString().replaceAll("SNAPSHOT", "${buildNumber}"))
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'http://redshift-maven-repository.s3-website-us-east-1.amazonaws.com/release' }
    }

    dependencyManagement {

        dependencies {
            dependency "org.opentestsystem.rdw.common:rdw-common-messaging:${rdwCommonVersion}"
            dependency "org.opentestsystem.rdw.common:rdw-common-group:${rdwCommonVersion}"
            dependency "org.opentestsystem.rdw.common:rdw-common-model:${rdwCommonVersion}"
            dependency "org.opentestsystem.rdw.common:rdw-common-utils:${rdwCommonVersion}"
            dependency "org.opentestsystem.rdw.common:rdw-common-security:${rdwCommonVersion}"
            dependency "org.opentestsystem.rdw.common:rdw-common-status:${rdwCommonVersion}"
            dependency "org.opentestsystem.rdw.common:rdw-common-archive:${rdwCommonVersion}"

            // commonly used libraries
            dependency 'com.google.guava:guava:23.0'
            dependency 'commons-io:commons-io:2.6'
            dependency 'io.jsonwebtoken:jjwt:0.9.0'
            dependency 'org.apache.pdfbox:pdfbox:2.0.7'
            dependency 'org.slf4j:slf4j-api:1.7.25'
            dependency 'org.thymeleaf:thymeleaf:3.0.6.RELEASE'

            // spring extensions aren't included in the the bom
            dependency 'org.springframework.security.extensions:spring-security-saml2-core:1.0.2.RELEASE'

            // commonly used test libraries
            dependency 'junit:junit:4.12'
            dependency 'org.assertj:assertj-core:3.6.2'
            dependency 'org.mockito:mockito-core:2.16.0'
        }
        imports {
            // https://github.com/spring-projects/spring-cloud/wiki/Spring-Cloud-Camden-Release-Notes
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Camden.SR7'
        }
    }

    dependencies {
        compile "org.slf4j:slf4j-api"

        testCompile 'junit:junit'
        testCompile 'org.assertj:assertj-core'
        testCompile 'org.mockito:mockito-core'
    }

    // task to generate build-info.properties with build.version, build.timestamp
    // using this approach instead of resource filtering to avoid modifying source
    task createBuildInfoFile(dependsOn: processResources) {
        doLast {
            def buildInfoFile = new File("${buildDir}/resources/main/build-info.properties")
            buildInfoFile.parentFile.mkdirs()
            Properties props = new Properties()
            props.setProperty('build.version', project.version.toString())
            props.store(buildInfoFile.newWriter(), null)
        }
    }

    /***************************************************************
     * Docker setup for all subprojects except rdw-reporting-common,
     *  which doesn't have a docker image
     ***************************************************************/

    if (!it.hasProperty('skipDocker')) {
        apply plugin: 'com.bmuschko.docker-remote-api'

        // trigger creation of build-info.properties
        classes.dependsOn(createBuildInfoFile)

        docker {
            registryCredentials {
                username = dockerHubUser
                password = dockerHubPassword
                email = dockerHubEmail
            }
        }

        // task to create Dockerfile
        // (http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/image/Dockerfile.html)
        task createDockerfile(type: Dockerfile) {
            destFile = project.file('build/docker/Dockerfile')
            from('openjdk:8-jre-alpine')
            volume('/tmp')
            label([maintainer: 'Fairway Technologies'])
            copyFile("${jar.archivePath.name}", "${jar.archivePath.name}")
            exposePort(8008, 8080)
            runCommand("apk add --no-cache tini curl jq")
            entryPoint("/sbin/tini", "--")
            environmentVariable("MAX_HEAP_SIZE", "-Xmx384m")
            instruction('CMD exec java $MAX_HEAP_SIZE $JAVA_OPTS -jar ' + "${jar.archivePath.name}")
        }

        // task to build the docker image
        task buildImage(type: DockerBuildImage, dependsOn: [build, createDockerfile]) {
            dockerFile = createDockerfile.destFile
            inputDir = project.file('build/docker/')
            tag = "${dockerPrefix}/${project.name}" + (project.hasProperty("release") ? ":${project.version}" : "")
            doFirst {
                copy {
                    from jar
                    into inputDir
                }
            }
        }

        // to use this, you must use valid docker credentials, which can be set in the gradle.properties file
        task dockerPushImage(type: DockerPushImage, dependsOn: buildImage) {
            imageName = "${dockerPrefix}/${project.name}"
        }
    }

    // Skip running integration and system tests automatically, because they take a long time
    test.exclude '**/*IT.*', '**/*RST.*'

    //This task for for manually running the integration tests against schemas being developed simultaneously with this
    // project, versus the schemas that are "fixed" under the RDW_Schema submodule in this project
    task manualIT(type: Test) {
        include '**/*IT.*'
        outputs.upToDateWhen { false }
        doFirst {
            println "Running Manual Integration Tests..."
        }
    }

    task IT(type: Test, dependsOn:[":cleanReportingTest", ":migrateReportingTest"]) {
        include '**/*IT.*'
        group = 'Verification'
        description = 'Runs the integration tests.'
        outputs.upToDateWhen { false }
        doLast {
            println "Running Integration Tests..."
        }
    }

    //Specific integration test task that will automatically migrate the test database schemas
    task moIT(type: Test, dependsOn:[":migrateReportingTest"]) {
        include '**/*IT.*'
        outputs.upToDateWhen { false }
        doFirst {
            println "Running Integration Tests without cleaning database (flyway migrate only)..."
        }
    }

    task coverage(type: Task, dependsOn: [test, IT]) {
        doFirst {
            jacocoTestReport.executionData(test, IT)
            jacocoTestReport.reports.html.destination = file("${reporting.baseDir}/${name}")
        }
        finalizedBy(jacocoTestReport)
    }
}

/********************************************
 * RDW_Schema artifact dependency setup
 *******************************************/

//Initializing some properties for use in extracting schema dependency and finding schema tasks
project.ext.schemaBaseDir =  "$buildDir/schema/"

//Creating a schema configuration for facilitating unzipping and copying sql scripts
configurations {
    rdwSchema
    repositories {
        mavenLocal()
    }
}

//Declaring root project dependency on schema
dependencies {
    rdwSchema("org.opentestsystem.rdw.schema:rdw-schema:${rdwSchemaVersion}@zip")
}

// Helper function to get the unzipped schema directory path at runtime.
def schemaDir() {
    schemaBaseDir + configurations.rdwSchema.singleFile.name.take(configurations.rdwSchema.singleFile.name.lastIndexOf('.'))
}

//Schema extraction task
task extractSchema(type: Sync) {
    dependsOn configurations.rdwSchema

    from { // use of closure defers evaluation until execution time
        zipTree(configurations.rdwSchema.singleFile)
    }
    into schemaBaseDir
}

/**********************************************************************
 * Dynamically creating the tasks to run against the database schema
 * The rdw-schema dependency contains a build.gradle file that defines
 *   specific tasks related to the schemas
 *********************************************************************/

//These are the tasks available in the Schema dependency that we would want to use in this project
def taskMap = [
        "cleanReporting": "Cleans the schemas that are required for running locally",
        "migrateReporting": "Migrates the schemas that are required for running locally",
        "cleanWarehouse": "Cleans the schemas that are required for running locally",
        "migrateWarehouse": "Migrates the schemas that are required for running locally",
        "cleanReporting_test": "Cleans the schemas that integration tests depend on",
        "migrateReporting_test": "Migrates the schemas that integration tests depend on",
        "cleanWarehouse_test": "Cleans the schemas that integration tests depend on",
        "migrateWarehouse_test": "Migrates the schemas that integration tests depend on",
        "cleanReporting_olap_test": "Cleans the schemas that redshift tests depend on",
        "migrateReporting_olap_test": "Migrates the schemas that redshift tests depend on",
        "cleanAll_test": "Cleans the schemas that integration tests depend on",
        "migrateAll_test": "Migrates the schemas that integration tests depend on",
]

taskMap.keySet().each { key ->
    def taskName = key.replaceAll("_test", "Test").replaceAll("_olap", "Olap")
    if (!taskName.endsWith("Test")) taskName += "Prod"
    task(taskName, type: GradleBuild, dependsOn: extractSchema) {
        group = "Schema"
        description = taskMap[key]
        doFirst {
            dir { schemaDir() }
            // need to copy any command-line properties for sub-build
            startParameter.projectProperties = gradle.startParameter.projectProperties
        }
        tasks = [key]
    }
}

/****************************
 * JFrog Artifactory setup
 *****************************/
artifactory {
    contextUrl = "https://airdev.jfrog.io/airdev"
    resolve {
        repository {
            repoKey = 'libs-releases'
            maven = true
        }
    }
}
