buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath('io.spring.gradle:dependency-management-plugin:1.0.0.RELEASE')
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE')
        classpath('org.springframework.build.gradle:propdeps-plugin:0.0.7')
    }
}

plugins {
    id "com.moowork.node" version "1.1.1"
    id "net.ltgt.apt" version "0.9"
    id "com.dorongold.task-tree" version "1.3"
}

apply plugin: 'idea'
apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'net.ltgt.apt'

apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.springframework.boot'

group = 'org.opentestsystem.rdw.reporting'
version = '0.0.1-SNAPSHOT'

description = """Reporting Service"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-aop' 
    compile 'org.springframework.boot:spring-boot-starter-actuator' 
    compile 'org.springframework.boot:spring-boot-starter-jdbc' 
    compile 'org.springframework.boot:spring-boot-starter-security' 
    compile 'org.springframework.boot:spring-boot-starter-web' 
    compile 'org.springframework.security.extensions:spring-security-saml2-core:1.0.2.RELEASE' 
    compile 'org.projectlombok:lombok:1.16.12' 
    compile 'com.google.guava:guava:20.0' 
    compile 'mysql:mysql-connector-java' 
    compile 'org.immutables:value:2.4.3' 
    runtime 'org.springframework.boot:spring-boot-devtools'
    testCompile('org.springframework.boot:spring-boot-starter-test') {
        exclude(module: 'commons-logging')
    }
    testCompile "junit:junit:4.11"
}

node {
    version = '7.5.0'
    npmVersion = '4.0.5'
    download = true
    workDir = file("${project.projectDir}/src/main/webapp")
    nodeModulesDir = file("${project.projectDir}/src/main/webapp")
}

//Hopefully this allows us to hot add resources
bootRun {
    addResources = true
    if ( project.hasProperty('jvmArgs') ) {
        jvmArgs project.jvmArgs.split('\\s+')
    }
}
bootRepackage {
    mainClass = 'org.opentestsystem.rdw.reporting.Application'
}

//This is the build task for the Front End (Fe) and only compiles when the inputs change
task buildFe(type: NpmTask) {
    inputs.files(fileTree('src/main/webapp/node_modules'))
    inputs.files(fileTree('src/main/webapp/src'))
    inputs.files(fileTree('src/main/webapp/e2e'))
    inputs.file('src/main/webapp/package.json')
    inputs.file('src/main/webapp/karma.conf.js')
    inputs.file('src/main/webapp/angular-cli.json')
    inputs.file('src/main/webapp/tslint.json')
    inputs.file('src/main/webapp/protractor.conf.js')

    outputs.dir('build/resources/static')

    dependsOn npm_install
    args = ['run', 'build']
}

// The processResources task takes care of gathering all of the resources in the project to the target directory. 
// In order to get the front end resources in to the final jar, the front end needs to be built
// before all of the other resources are processed in order to get the static resouces in the right place
processResources.dependsOn buildFe
