plugins {
    id "com.moowork.node" version "1.1.1"
}

apply plugin: 'org.springframework.boot'

createDockerfile {
    ext.java_opts = ["-Xmx512m"]
}

dependencies {
    compile project(':rdw-admin-common')
    compile 'org.opentestsystem.rdw.common:rdw-common-archive'
    compile 'org.opentestsystem.rdw.common:rdw-common-model'
    compile 'org.opentestsystem.rdw.common:rdw-common-security'
    compile 'org.opentestsystem.rdw.common:rdw-common-status'
    compile 'org.opentestsystem.rdw.common:rdw-common-utils'
    optional 'org.springframework.boot:spring-boot-configuration-processor'
    runtime 'org.springframework.boot:spring-boot-devtools'
    compile 'org.springframework.cloud:spring-cloud-starter-aws'
    compile 'org.springframework.boot:spring-boot-starter-aop'
    compile 'org.springframework.boot:spring-boot-starter-jdbc'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.cloud:spring-cloud-starter-config'
    compile 'org.springframework.cloud:spring-cloud-starter-stream-rabbit'
    compile 'org.springframework.cloud:spring-cloud-stream'
    compile 'org.springframework.retry:spring-retry'
    compile 'org.springframework.security.extensions:spring-security-saml2-core'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    compile 'com.google.guava:guava'
    compile 'mysql:mysql-connector-java'
    compile 'org.apache.commons:commons-io'
    compile 'org.opentestsystem.rdw.common:rdw-common-archive'
    compile 'org.opentestsystem.rdw.common:rdw-common-messaging'
    compile 'org.opentestsystem.rdw.common:rdw-common-model'
    compile 'org.opentestsystem.rdw.common:rdw-common-status'
    compile 'org.opentestsystem.rdw.common:rdw-common-utils'

    compile 'org.apache.commons:commons-csv:+'

    compile 'com.google.code.gson:gson'

    testCompile project(path: ':rdw-admin-common', configuration: 'tests')
    testCompile('org.springframework.boot:spring-boot-starter-test') {
        exclude(module: 'commons-logging')
    }
    testCompile 'org.springframework.security:spring-security-test'
    testCompile 'org.springframework.cloud:spring-cloud-stream-test-support'
}

node {
    version = '7.10.0'
    npmVersion = '4.5.0'
    download = true
    workDir = file("${project.projectDir}/src/main/webapp")
    nodeModulesDir = file("${project.projectDir}/src/main/webapp")
}

//Hopefully this allows us to hot add resources
bootRun {
    addResources = true
    if (project.hasProperty('jvmArgs')) {
        jvmArgs project.jvmArgs.split('\\s+')
    }
}

// generate coverage report automatically (./build/reports/jacoco/test/html/index.html)
test.finalizedBy(jacocoTestReport)

//This is the build task for the Front End (Fe) and only compiles when the inputs change
task buildFe(type: NpmTask) {
    inputs.files(fileTree("src/main/webapp/src"))
    inputs.file("src/main/webapp/package.json")
    workingDir = file ("src/main/webapp")
    outputs.dir('build/resources/static')
    dependsOn npm_install
    args = ['run', 'build']
}

task testFe(type: NpmTask) {
    inputs.files(fileTree("src/main/webapp/src"))
    inputs.file("src/main/webapp/package.json")
    workingDir = file ("src/main/webapp")
    outputs.dir('build/resources/static')
    dependsOn npm_install
    args = ['run', 'test-ci']
}

// The processResources task takes care of gathering all of the resources in the project to the target directory.
// In order to get the front end resources in to the final jar, the front end needs to be built
// before all of the other resources are processed in order to get the static resouces in the right place
processResources.dependsOn buildFe